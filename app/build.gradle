plugins {
    id "com.google.protobuf" version "0.9.4"
    id 'kotlin-kapt'
 // id 'com.google.dagger.hilt.android'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def versionMajor = 0
def versionMinor = 88
def versionPatch = 0
def acraVersion = "5.11.2"


android {
    namespace "com.aatorque.stats"
    compileSdk 33
    defaultConfig {
        applicationId "com.aatorque.stats"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "boolean", "SIMULATE_METRICS", "false"
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
            buildConfigField "boolean", "SIMULATE_METRICS", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version", "${defaultConfig.versionName}"
            signingConfig signingConfigs.debug
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }

    kotlinOptions {
        jvmTarget = "18"
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

configurations.all {
    exclude group: 'com.google.guava', module: 'guava-jdk5'
}

configurations {
    cleanedAnnotations
    implementation.exclude group: 'org.jetbrains' , module:'annotations'
}


dependencies {
    // Replaced with updated SDK from "https://github.com/cupral/aauto-sdk.git" (as git-submodule)
    //implementation 'com.github.martoreto:aauto-sdk:v4.7'

    implementation 'com.github.martoreto:aauto-sdk:v4.7'
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation 'com.github.christophesmet:android_maskable_layout:v1.3.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.github.anastr:speedviewlib:1.4.0'
    implementation 'com.austingreco:image-list-preference:1.0.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    //noinspection GradleDependency
    implementation 'com.google.guava:guava:32.1.2-jre'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    implementation 'com.google.android.gms:play-services-identity:18.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    testImplementation 'junit:junit:4.13.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.10"
    implementation 'com.ezylang:EvalEx:3.0.5'

    implementation "androidx.datastore:datastore:1.0.0"
    implementation  "com.google.protobuf:protobuf-javalite:3.18.0"
   // implementation 'com.google.dagger:hilt-android:2.48'
  //  kapt 'com.google.dagger:hilt-compiler:2.48'
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-toast:$acraVersion"
}

protobuf {
    // Configures the Protobuf compilation and the protoc executable
    protoc {
        // Downloads from the repositories
        artifact = "com.google.protobuf:protoc:3.14.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // Configures the task output type
                java {
                    // Java Lite has smaller code size and is recommended for Android
                    option 'lite'
                }
            }
        }
    }
}

kapt {
    correctErrorTypes true
}

//apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
    jcenter()
}
